{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Calculate code distances and more using tensor networks"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "operator_weights_plot"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using ITensors, Random, StatsPlots, NBInclude, LightGraphs, GraphPlot\n",
    "# using SimpleCodes, TNCodes\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    OperatorWeights\n",
    "\n",
    "Stores operator weight distribution for stabilizers, all code operators\n",
    "(stabilizers + all logical representatives), as well as distance.\n",
    "\"\"\"\n",
    "struct OperatorWeights\n",
    "    stabilizer_weights::Array{Int64,1}\n",
    "    all_operator_weights::Array{Int64,1}\n",
    "    distance::Int64\n",
    "    \n",
    "    function OperatorWeights(stabilizer_weights,all_operator_weights)\n",
    "        logical_weights = all_operator_weights - stabilizer_weights\n",
    "        index = findfirst(x->x>0,logical_weights)\n",
    "\n",
    "        distance =  index -1\n",
    "        new(stabilizer_weights,all_operator_weights,distance)\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "OperatorWeights() = OperatorWeights(Int64[0],Int64[1])\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    simple_weight_tensor(physical_indices,out_index)\n",
    "\n",
    "Returns a tensor that, when contracted with a code tensor, gives the weights\n",
    "of all Pauli operators described by that code tensor.\n",
    "\"\"\"\n",
    "function simple_weight_tensor(\n",
    "        physical_indices::Array{Index{Int64},1},\n",
    "        out_index::Index{Int64})\n",
    "    \n",
    "    all_indices = vcat(physical_indices,[out_index])\n",
    "    dims = dim.(all_indices)\n",
    "    array = fill(0,dims...)\n",
    "    \n",
    "    n = length(physical_indices)  \n",
    "    \n",
    "    for γ in 0:4^n-1\n",
    "        index = digits!(zeros(Int64,n),γ,base=4)\n",
    "        w = weight(index)\n",
    "        index = index .+ 1\n",
    "\n",
    "        full_index = vcat(index,w+1)\n",
    "        array[full_index...] = 1\n",
    "    end    \n",
    "    \n",
    "    return ITensor(array,all_indices...)\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    addition_tensor(in_index1,in_index2,out_index)\n",
    "\n",
    "Gives a tensor that is one if `out_index` equals  the sum of `in_index1`\n",
    "and `in_index2`.  Warning: since Julia is one indexed, zero is replaced by\n",
    "1, so that the tensor is nonzero if `in_index1 + in_index2 - 1 = out_index`.\n",
    "\"\"\"\n",
    "function addition_tensor(in_index1,in_index2,out_index)\n",
    "    \n",
    "    in_dim1 = dim(in_index1)\n",
    "    in_dim2 = dim(in_index2)\n",
    "    out_dim = dim(out_index)\n",
    "    dims = [in_dim1,in_dim2,out_dim]\n",
    "    \n",
    "    array = fill(0,dims...)\n",
    "    \n",
    "    for α in 1:in_dim1, β in 1:in_dim2, γ in 1:out_dim\n",
    "        if α + β - 2 == γ - 1\n",
    "            array[α,β,γ] = 1\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return ITensor(array,in_index1,in_index2,out_index) \n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "function addition_tensor(in_index1,in_index2)\n",
    "    \n",
    "    in_dim1 = dim(in_index1)\n",
    "    in_dim2 = dim(in_index2)\n",
    "    out_dim = in_dim1 + in_dim2 -2 + 1  #(indices start from zero in Julia) \n",
    "    out_index = Index(out_dim,\"out_index\")\n",
    "    \n",
    "    return addition_tensor(in_index1,in_index2,out_index)\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    TN_weights(code;cosets=all_cosets)\n",
    "\n",
    "Returns a tensor describing the weights of all operators in the `cosets` of\n",
    "the code.  (`cosets` is applied to each `seed_code` separately.)\n",
    "\"\"\"\n",
    "function TN_weights(\n",
    "        code::TNCode;\n",
    "        cosets=all_cosets,\n",
    "        truncate_to=size(code)+1)\n",
    "    \n",
    "    code_tensors = ITensor[]\n",
    "    \n",
    "    for node in nodes(code)\n",
    "        if node < 0\n",
    "            code_type = node_types(code,node)\n",
    "            seed_code = code.seed_codes[code_type]\n",
    "            \n",
    "            k = length(seed_code.logicals)/2\n",
    "            logical_indices = [Index(4,\"logical\") for _ in 1:k]\n",
    "            physical_indices = node_indices(code,node)\n",
    "            code_tensor = code_to_Itensor(seed_code,logical_indices,physical_indices)\n",
    "            \n",
    "            code_tensor = cosets(code_tensor)\n",
    "            \n",
    "            push!(code_tensors,code_tensor)\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    for α in 1:length(code_tensors)\n",
    "        indices = inds(code_tensors[α])  #tuple\n",
    "        physical_indices = filter(x->hastags(x,\"physical\"),indices)\n",
    "        num_phys = length(physical_indices)\n",
    "        output_index = Index(num_phys + 1,\"weight\")\n",
    "        \n",
    "        local_weight_tensor = simple_weight_tensor([physical_indices...],output_index)\n",
    "        code_tensors[α] = code_tensors[α]*local_weight_tensor\n",
    "    end\n",
    "    \n",
    "    \n",
    "    indices = inds(code_tensors[1])\n",
    "    β = findfirst(x->hastags(x,\"weight\"),indices)\n",
    "    in_index1 = indices[β]\n",
    "    \n",
    "    for α in 2:length(code_tensors)\n",
    "        indices = inds(code_tensors[α])\n",
    "        β = findfirst(x->hastags(x,\"weight\"),indices)\n",
    "        in_index2 = indices[β]\n",
    "\n",
    "        in_dim1 = dim(in_index1)\n",
    "        in_dim2 = dim(in_index2)\n",
    "        total_out_dim = in_dim1 + in_dim2 -2 + 1\n",
    "        \n",
    "        if total_out_dim <= truncate_to\n",
    "            out_dim = total_out_dim\n",
    "        else\n",
    "            out_dim = truncate_to\n",
    "        end\n",
    "        \n",
    "        out_index = Index(out_dim,\"outind\")        \n",
    "        \n",
    "        add_tensor = addition_tensor(in_index1,in_index2,out_index)\n",
    "        code_tensors[α] = code_tensors[α] * add_tensor\n",
    "        \n",
    "        in_index1 = out_index\n",
    "    end\n",
    "\n",
    "    return ITensors.contract(code_tensors)\n",
    "\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "   TN_operator_weights(code)\n",
    "\n",
    "Returns `OperatorWeights` which includes the number of stabilizers\n",
    "of each weight, as well as the number of logical representatives\n",
    "(excluding identity) of each weight, as well as the code distance.\n",
    "\"\"\"\n",
    "function TN_operator_weights(\n",
    "        code::TNCode;\n",
    "        truncate_to=size(code)+1)\n",
    "    \n",
    "    stabilizer_weights = array(TN_weights(\n",
    "            code,\n",
    "            cosets=identity_coset,\n",
    "        truncate_to=truncate_to))\n",
    "    \n",
    "    all_operator_weights = array(TN_weights(\n",
    "            code,\n",
    "            cosets=all_cosets,\n",
    "            truncate_to=truncate_to))\n",
    "    \n",
    "    return OperatorWeights(stabilizer_weights,all_operator_weights)\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "   TN_distance(code)\n",
    "\n",
    "Returns the code distance calculated by contracting a tensor network.\n",
    "\"\"\"\n",
    "function TN_distance(\n",
    "        code::TNCode;\n",
    "        truncate_to=size(code)+1)\n",
    "\n",
    "    op_weights = TN_operator_weights(\n",
    "        code::TNCode;\n",
    "        truncate_to=truncate_to)\n",
    "    \n",
    "    return op_weights.distance\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    operator_weights_plot(weights)\n",
    "\n",
    "Plots a bar plot of the number of operators of each weight.\n",
    "This includes all code operators, all stabilizers, and all\n",
    "non-identity logicals.\n",
    "\"\"\"\n",
    "function operator_weights_plot(\n",
    "        weights::OperatorWeights;\n",
    "        truncate_to = length(weights.stabilizer_weights))\n",
    "    \n",
    "    all_ops = weights.all_operator_weights[1:truncate_to]\n",
    "    stabs = weights.stabilizer_weights[1:truncate_to]\n",
    "    \n",
    "    max_y_val = maximum(all_ops)\n",
    "    \n",
    "    logical_weights = all_ops - stabs\n",
    "    \n",
    "    # for log plot, so zero doesn't give -infinity\n",
    "    logical_weights = logical_weights .+ 0.001\n",
    "    all_ops = all_ops .+ 0.001\n",
    "    stabs = stabs .+ 0.001\n",
    "    \n",
    "    bar_data = hcat(\n",
    "        stabs,\n",
    "        logical_weights)\n",
    "#         all_ops)\n",
    "    \n",
    "    L = length(logical_weights)\n",
    "\n",
    "    labels = repeat(\n",
    "        [\"Stabilizers\", \"Logicals\"],\n",
    "#             \"Logicals + stabilizers\"],\n",
    "        inner = L)\n",
    "\n",
    "    return groupedbar(\n",
    "        repeat(collect(0:L-1),outer = 2),\n",
    "        bar_data,\n",
    "        bar_position = :dodge,\n",
    "        group = labels, \n",
    "        xlabel = \"Weight\",\n",
    "        ylabel = \"Number\",\n",
    "        yaxis=:log,\n",
    "        title = \"Number of operators of given weight\",\n",
    "        bar_width = 0.67,\n",
    "        lw = 0,\n",
    "        framestyle = :box,\n",
    "        legend = :topleft,\n",
    "        ylims = (0.1,10*max_y_val),\n",
    "        xticks = (0:L-1))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.6.1",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}

{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "compare_code_success_predicted (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# using ITensors, LightGraphs, GraphPlot, NBInclude\n",
    "using ITensors\n",
    "using TensorNetworkCodes\n",
    "using Statistics:mean, stdm\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    physical_tensor(index,error_prob,pauli)\n",
    "\n",
    "Returns a one leg `ITensor` describing the error on one site.\n",
    "\"\"\"\n",
    "function physical_tensor(\n",
    "        index::Index{Int64},\n",
    "        error_prob::Float64,\n",
    "        pauli::Int64)\n",
    "\n",
    "    array = fill(error_prob/3,4)\n",
    "    array[pauli + 1] = 1 - error_prob\n",
    "\n",
    "    return ITensor(array,index)\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    create_virtual_tensor(graph,node)\n",
    "\n",
    "Creates the `ITensor` describing the seed code at `node`.\n",
    "\"\"\"\n",
    "function create_virtual_tensor(code::TNCode,node::Int64)\n",
    "    \n",
    "    graph = code.code_graph\n",
    "    \n",
    "    indices = node_indices(code,node)\n",
    "    code_type = node_types(code,node)\n",
    "    seed_code = code.seed_codes[code_type]\n",
    "    \n",
    "    k = size(seed_code) - length(seed_code.stabilizers)\n",
    "    logical_indices = [Index(4,\"logical\") for α in 1:k]\n",
    "    \n",
    "    return code_to_Itensor(\n",
    "        seed_code,\n",
    "        logical_indices,\n",
    "        indices)\n",
    "    \n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "########## Should be in TN_code_types.jl\n",
    "\n",
    "function physical_neighbours(code,node)\n",
    "\n",
    "    all_edges = TensorNetworkCodes.edges(code)\n",
    "    neighbour_edges = filter(x->node in x,all_edges)\n",
    "    neighbour_nodes = union(neighbour_edges...)\n",
    "    \n",
    "    return filter(x->x>0,neighbour_nodes)\n",
    "    \n",
    "end\n",
    "\n",
    "##########\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    basic_contract\n",
    "\n",
    "Given a matrix of ITensors (`imatrix`), contracts exactly working\n",
    "across row by row.\n",
    "\"\"\"\n",
    "function basic_contract(imatrix; bond_dim = 10)\n",
    "    output = ITensor(1)\n",
    "    for i in 1:size(imatrix)[1], j in 1:size(imatrix)[2]\n",
    "        output = output * imatrix[i,j]\n",
    "    end\n",
    "            \n",
    "    return output\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    MPS_MPO_contract\n",
    "\n",
    "Given a matrix of ITensors (`imatrix`), contracts approximately by\n",
    "treating the first row as an MPS and the subsequent rows as MPOs.\n",
    "\"\"\"\n",
    "function MPS_MPO_contract(imatrix; bond_dim = 10)\n",
    "    ψ = MPS(imatrix[1,:])\n",
    "    for j in 2:size(imatrix)[1]-1\n",
    "        Σ = MPO(imatrix[j,:])\n",
    "        ψ = ITensors.contract(Σ, ψ; maxdim = bond_dim)\n",
    "    end\n",
    "    \n",
    "    output = imatrix[end,1] * ψ[1]\n",
    "    for j in 2:size(imatrix)[2]\n",
    "        output = output * imatrix[end,j] * ψ[j]\n",
    "    end\n",
    "        \n",
    "    return output\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    contract(\n",
    "        code,\n",
    "        pure_error::Array{Int64,1},\n",
    "        error_prob::Float64;\n",
    "        contraction_function=basic_contract,\n",
    "        bond_dim = 10)\n",
    "\n",
    "Contracts the tensor network described by `code_graph`.  Uses any\n",
    "specified `contraction_function`.\n",
    "\"\"\"\n",
    "function contract(\n",
    "        code,\n",
    "        pure_error::Array{Int64,1},\n",
    "        error_prob::Float64;\n",
    "        contraction_function=basic_contract,\n",
    "        bond_dim = 10)\n",
    "    \n",
    "    L = Int64(sqrt(size(code)))\n",
    "    imatrix = [ITensor(1) for i in 1:L, j in 1:L]\n",
    "    \n",
    "    for i in 1:L, j in 1:L\n",
    "        node =  -(L*(i-1) + j)\n",
    "        tensor = create_virtual_tensor(code,node)\n",
    "        \n",
    "        neighbours = physical_neighbours(code,node)\n",
    "        for neighbour in neighbours\n",
    "            indices = edge_indices(code,Set([node,neighbour]))\n",
    "            α = findfirst(x->hastags(x,\"physical\"),indices)\n",
    "            index = indices[α]\n",
    "            tensor = tensor * \n",
    "            physical_tensor(index,error_prob,pure_error[neighbour])\n",
    "        end\n",
    "        imatrix[i,j] = tensor\n",
    "    end\n",
    "   \n",
    "    return contraction_function(imatrix,bond_dim=bond_dim)\n",
    "    \n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    TN_decoder(\n",
    "        code,\n",
    "        syndrome,\n",
    "        error_prob;\n",
    "        error_model = \"depolarizing\",\n",
    "        contraction_function = simple_contract)   \n",
    "\n",
    "Decodes a `TNCode` given the `syndrome` and a choice of `TN_contraction_function`.\n",
    "\"\"\"\n",
    "function TN_decoder(\n",
    "        code::TNCode,\n",
    "        syndrome::Array{Int64,1},\n",
    "        error_prob::Float64;\n",
    "        error_model = \"depolarizing\",\n",
    "        contraction_function = basic_contract,\n",
    "        bond_dim=10)   \n",
    "    \n",
    "    # physical tensors\n",
    "    pure_error = get_pure_error(code,syndrome)\n",
    "    \n",
    "    coset_probabilities = array(\n",
    "        contract(\n",
    "        code,\n",
    "        pure_error,\n",
    "        error_prob;\n",
    "        contraction_function=contraction_function,\n",
    "        bond_dim = bond_dim)\n",
    "        )\n",
    "    \n",
    "    if length(coset_probabilities) != 4\n",
    "        error(\"decoder not set up for multiple logicals yet!\")\n",
    "    end\n",
    "    \n",
    "    coset_probabilities = abs.(coset_probabilities)\n",
    "    \n",
    "    best_coset = argmax(coset_probabilities) - 1\n",
    "    powers = [0,0]\n",
    "    if best_coset in [1,2]\n",
    "        powers[1] = 1\n",
    "    end\n",
    "    if best_coset in [3,2]\n",
    "        powers[2] = 1\n",
    "    end\n",
    "\n",
    "    correction = TensorNetworkCodes.product_with_powers(code.logicals,powers)\n",
    "    correction = pauli_product.(correction,pure_error)\n",
    "    \n",
    "    return correction   \n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    TN_decoding_success_prob(\n",
    "        code,\n",
    "        syndrome,\n",
    "        error_prob;\n",
    "        error_model = \"depolarizing\",\n",
    "        contraction_function = basic_contract,\n",
    "        bond_dim=10)   \n",
    "\n",
    "For a `TNCode` given error `syndrome` and a choice of `contraction_function`,\n",
    "finds the probability of successfully decoding.\n",
    "\"\"\"\n",
    "function TN_decoding_success_prob(\n",
    "        code::TNCode,\n",
    "        syndrome::Array{Int64,1},\n",
    "        error_prob::Float64;\n",
    "        error_model = \"depolarizing\",\n",
    "        contraction_function = basic_contract,\n",
    "        bond_dim=10)   \n",
    "    \n",
    "    # physical tensors\n",
    "    pure_error = get_pure_error(code,syndrome)\n",
    "    \n",
    "    coset_probabilities = array(\n",
    "        contract(\n",
    "        code,\n",
    "        pure_error,\n",
    "        error_prob;\n",
    "        contraction_function=contraction_function,\n",
    "        bond_dim = bond_dim)\n",
    "        )\n",
    "    \n",
    "    coset_probabilities = abs.(coset_probabilities)\n",
    "    best_coset = argmax(coset_probabilities)\n",
    "    \n",
    "    return coset_probabilities[best_coset]/(sum(coset_probabilities))\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "function compare_code_success_empirical(\n",
    "        code1,\n",
    "        code2,\n",
    "        error_probabilities,\n",
    "        num_iter;\n",
    "        contraction_function = basic_contract,\n",
    "        χ=10)\n",
    "\n",
    "    \n",
    "    P = length(error_probabilities)\n",
    "    code1_success = zeros(P)\n",
    "    code2_success = zeros(P)\n",
    "    \n",
    "    n = size(code1)\n",
    "    if n != size(code2)\n",
    "        error(\"different size codes\")\n",
    "    end\n",
    "    \n",
    "    for α in 1:P\n",
    "        for _ in 1:num_iter\n",
    "            error_prob = error_probabilities[α]\n",
    "\n",
    "            error = random_pauli_error(n,error_prob) \n",
    "            \n",
    "            # code 1\n",
    "            syndrome1 = get_syndrome(code1,error)            \n",
    "            \n",
    "            code1_correction = TN_decoder(\n",
    "                code1,\n",
    "                syndrome1,\n",
    "                error_prob;\n",
    "                contraction_function = basic_contract,\n",
    "                bond_dim=χ)\n",
    "\n",
    "            effect_on_code = pauli_product.(error,code1_correction)\n",
    "            if (do_they_commute.(Ref(effect_on_code),code1.logicals) \n",
    "                == zeros(Int64,length(code1.logicals)))\n",
    "                code1_success[α] += 1\n",
    "            end\n",
    "            \n",
    "            # code 2\n",
    "            syndrome2 = get_syndrome(code2,error)            \n",
    "            \n",
    "            code2_correction = TN_decoder(\n",
    "                code2,\n",
    "                syndrome2,\n",
    "                error_prob;\n",
    "                contraction_function = MPS_MPO_contract,\n",
    "                bond_dim=χ)\n",
    "\n",
    "            effect_on_code = pauli_product.(error,code2_correction)\n",
    "            if (do_they_commute.(Ref(effect_on_code),code2.logicals) \n",
    "                == zeros(Int64,length(code2.logicals)))\n",
    "                code2_success[α] += 1\n",
    "            end\n",
    "            \n",
    "\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return [code1_success ./num_iter,code2_success ./num_iter]\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "function compare_code_success_predicted(\n",
    "        code1,\n",
    "        code2,\n",
    "        error_probabilities,\n",
    "        num_iter;\n",
    "        contraction_function = basic_contract,\n",
    "        χ=10)\n",
    "\n",
    "    P = length(error_probabilities)\n",
    "    code1_success = zeros(P)\n",
    "    code1_stderr = zeros(P)\n",
    "    code2_success = zeros(P)\n",
    "    code2_stderr = zeros(P)\n",
    "    \n",
    "    n = size(code1)\n",
    "    if n != size(code2)\n",
    "        error(\"different size codes\")\n",
    "    end\n",
    "    \n",
    "    for α in 1:P\n",
    "        data1 = zeros(num_iter)\n",
    "        data2 = zeros(num_iter)\n",
    "        \n",
    "        for β in 1:num_iter\n",
    "            error_prob = error_probabilities[α]\n",
    "            error = random_pauli_error(n,error_prob) \n",
    "            \n",
    "            # code 1\n",
    "            syndrome1 = get_syndrome(code1,error)            \n",
    "            \n",
    "            code1_succ = TN_decoding_success_prob(\n",
    "                code1,\n",
    "                syndrome1,\n",
    "                error_prob;\n",
    "                error_model = \"depolarizing\",\n",
    "                contraction_function = basic_contract,\n",
    "                bond_dim=χ) \n",
    "           \n",
    "            data1[β] = code1_succ\n",
    "            \n",
    "            # code 2\n",
    "            syndrome2 = get_syndrome(code2,error)            \n",
    "            \n",
    "            code2_succ = TN_decoding_success_prob(\n",
    "                code2,\n",
    "                syndrome2,\n",
    "                error_prob;\n",
    "                error_model = \"depolarizing\",\n",
    "                contraction_function = basic_contract,\n",
    "                bond_dim=χ) \n",
    "           \n",
    "            data2[β] = code2_succ\n",
    "\n",
    "        end\n",
    "        \n",
    "        code1_success[α] = mean(data1)\n",
    "        code1_stderr[α] = stdm(data1,mean(data1))\n",
    "        \n",
    "        code2_success[α] = mean(data2)\n",
    "        code2_stderr[α] = stdm(data2,mean(data2))\n",
    "        \n",
    "    end\n",
    "    \n",
    "    return [code1_success,code1_stderr ./ sqrt(num_iter),\n",
    "        code2_success,code2_stderr ./ sqrt(num_iter)]\n",
    "    # really it's only stderr (standard error) now after the division\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Testing"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "function compare_decoders(code,χ,num_iter)\n",
    "\n",
    "    error_probabilities = [0.0,0.02,0.04,0.06,0.08,0.10,0.12,0.14,0.16,0.18]\n",
    "    P = length(error_probabilities)\n",
    "    basic_success = zeros(P)\n",
    "    mps_success = zeros(P)\n",
    "    trg_success = zeros(P)\n",
    "    \n",
    "    n = size(code)\n",
    "    for α in 1:P\n",
    "        for _ in 1:num_iter\n",
    "            error_prob = error_probabilities[α]\n",
    "\n",
    "            error = random_pauli_error(n,error_prob) \n",
    "            syndrome = get_syndrome(code,error)\n",
    "            \n",
    "            \n",
    "            basic_correction = TN_decoder(\n",
    "                code,\n",
    "                syndrome,\n",
    "                error_prob;\n",
    "                contraction_function = basic_contract,\n",
    "                bond_dim=χ)\n",
    "\n",
    "            effect_on_code = pauli_product.(error,basic_correction)\n",
    "            if (do_they_commute.(Ref(effect_on_code),code.logicals) \n",
    "                == zeros(Int64,length(code.logicals)))\n",
    "                basic_success[α] += 1\n",
    "            end\n",
    "            \n",
    "            \n",
    "            mps_correction = TN_decoder(\n",
    "                code,\n",
    "                syndrome,\n",
    "                error_prob;\n",
    "                contraction_function = MPS_MPO_contract,\n",
    "                bond_dim=χ)\n",
    "\n",
    "            effect_on_code = pauli_product.(error,mps_correction)\n",
    "            if (do_they_commute.(Ref(effect_on_code),code.logicals) \n",
    "                == zeros(Int64,length(code.logicals)))\n",
    "                mps_success[α] += 1\n",
    "            end\n",
    "            \n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return [basic_success ./num_iter,mps_success ./num_iter]\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "\n",
    "# □ = rotated_surface_code(7);\n",
    "# χ = 35\n",
    "# N = 20\n",
    "\n",
    "# @time compare_decoders(□,χ,N)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      " 47.823992 seconds (23.47 M allocations: 41.755 GiB, 14.43% gc time)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "2-element Vector{Vector{Float64}}:\n",
       " [1.0, 1.0, 1.0, 1.0, 1.0, 1.0, 0.85, 1.0, 0.95, 0.75]\n",
       " [1.0, 1.0, 1.0, 1.0, 1.0, 1.0, 0.85, 1.0, 0.95, 0.75]"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# using NBInclude\n",
    "# @nbinclude(\"Rotated_and_modified_surface_codes.ipynb\")\n",
    "\n",
    "# □ = central_rotated_surface_code(7);\n",
    "# χ = 35\n",
    "# N = 20\n",
    "\n",
    "# @time compare_decoders(□,χ,N)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.6.3",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
